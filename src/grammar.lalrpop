use crate::lexer;
use crate::ast;

grammar<'input>;

extern {
    type Location = usize;
    type Error = lexer::Error;

    enum lexer::Token<'input> {
        string => lexer::Token::StringLiteral(<String>),
        cstring => lexer::Token::CStringLiteral(<String>),

        int => lexer::Token::IntLit(<i32>),
        uint => lexer::Token::UIntLit(<u32>),
        int8 => lexer::Token::Int8Lit(<i8>),
        int16 => lexer::Token::Int16Lit(<i16>),
        int32 => lexer::Token::Int32Lit(<i32>),
        int64 => lexer::Token::Int64Lit(<i64>),
        uint8 => lexer::Token::UInt8Lit(<u8>),
        uint16 => lexer::Token::UInt16Lit(<u16>),
        uint32 => lexer::Token::UInt32Lit(<u32>),
        uint64 => lexer::Token::UInt64Lit(<u64>),

        // int => lexer::Token::IntLit(<&'input str>),
        // uint => lexer::Token::UIntLit(<&'input str>),
        // int8 => lexer::Token::Int8Lit(<&'input str>),
        // int16 => lexer::Token::Int16Lit(<&'input str>),
        // int32 => lexer::Token::Int32Lit(<&'input str>),
        // int64 => lexer::Token::Int64Lit(<&'input str>),
        // uint8 => lexer::Token::UInt8Lit(<&'input str>),
        // uint16 => lexer::Token::UInt16Lit(<&'input str>),
        // uint32 => lexer::Token::UInt32Lit(<&'input str>),
        // uint64 => lexer::Token::UInt64Lit(<&'input str>),

        ident => lexer::Token::Ident(<&'input str>),
        // "\n" => lexer::Token::Newline,

        "as" => lexer::Token::As,
        "const" => lexer::Token::Const,
        "var" => lexer::Token::Var,
        "->" => lexer::Token::Arrow,
        "func" => lexer::Token::Func,
        "extern" => lexer::Token::Extern,
        "return" => lexer::Token::Return,
        "class" => lexer::Token::Class,

        "&" => lexer::Token::Ampersand,
        "," => lexer::Token::Comma,

        "if" => lexer::Token::If,
        "else" => lexer::Token::Else,
        "true" => lexer::Token::True,
        "false" => lexer::Token::False,
        "and" => lexer::Token::And,
        "or" => lexer::Token::Or,
        "not" => lexer::Token::Not,
        "while" => lexer::Token::While,

        "(" => lexer::Token::LParen,
        ")" => lexer::Token::RParen,
        "{" => lexer::Token::LCurly,
        "}" => lexer::Token::RCurly,
        "[" => lexer::Token::LSquare,
        "]" => lexer::Token::RSquare,
        ":" => lexer::Token::Colon,
        ";" => lexer::Token::Semicolon,
        "=" => lexer::Token::Equals,

        // Comparison
        ">" => lexer::Token::Gt,
        ">=" => lexer::Token::Gte,
        "<" => lexer::Token::Lt,
        "<=" => lexer::Token::Lte,
        "==" => lexer::Token::IsEqual,
        "!=" => lexer::Token::NotEqual,

        // Math
        "+" => lexer::Token::Plus,
        "-" => lexer::Token::Minus,
        "*" => lexer::Token::Times,
        "/" => lexer::Token::Divide,
        "%" => lexer::Token::Percent,

        // Types
        "int" => lexer::Token::Int,
        "int8" => lexer::Token::Int8,
        "int16" => lexer::Token::Int16,
        "int32" => lexer::Token::Int32,
        "int64" => lexer::Token::Int64,
        "uint" => lexer::Token::UInt,
        "uint8" => lexer::Token::UInt8,
        "uint16" => lexer::Token::UInt16,
        "uint32" => lexer::Token::UInt32,
        "uint64" => lexer::Token::UInt64,
        "char" => lexer::Token::Char,
        "str" => lexer::Token::Str,
        "cstr" => lexer::Token::CStr,
    }
}

pub Program: ast::Program = <TopLevelStmt*> => ast::Program(<>);

TopLevelStmt: ast::TopLevelStmt = {
    <ClassDef> => ast::TopLevelStmt::ClassDef(<>),
    <FunctionDef> => ast::TopLevelStmt::FunctionDef(<>),
    <GlobalConstDef> => ast::TopLevelStmt::ConstDef(<>),
    <ExternDef> => ast::TopLevelStmt::ExternDef(<>),
}

ClassDef: ast::ClassDef =
    "class" <name:Ident> "{" <f:MaybeParamsList> "}" => {
        let fields = f.into_iter().map(|b| (b.name, b.ty)).collect();

        ast::ClassDef {
            name,
            fields,
        }
    };

pub FunctionDef: ast::FunctionDef = "func" <name:Ident> "(" <params:MaybeParamsList> ")" <return_type:("->" <Type>)?> <body:BlockStmt> => ast::FunctionDef {<>};

ExternDef: ast::ExternDef = "extern" <name:Ident> "(" <params:MaybeParamsList> ")" <return_type:("->" <Type>)?> ";" => ast::ExternDef {<>};

MaybeParamsList: Vec<ast::TypeBinding> = <ParamsList?> => <>.unwrap_or(vec![]);

ParamsList: Vec<ast::TypeBinding> = {
    <mut a:ParamsList> ","  <b:TypeBinding> => {a.push(b); a},
    <TypeBinding> => vec![<>],
}

GlobalConstDef: ast::GlobalConstDef =
    "const" <binding:TypeBinding> "=" <value:Literal> ";" => ast::GlobalConstDef {<>};

// STATEMENTS

pub Stmt: ast::Stmt = {
    "return" <Expr> ";" => ast::Stmt::ReturnStmt(<>),
    <Expr> ";" => ast::Stmt::ExprStmt(<>),
    <BlockStmt> => ast::Stmt::BlockStmt(<>),
    <IfStmt> => ast::Stmt::IfStmt(<>),
    <WhileStmt>,
    <ConstDef> => ast::Stmt::ConstDef(<>),
    <VarDef> => ast::Stmt::VarDef(<>),
}

BlockStmt: ast::BlockStmt =
    "{" <Stmt*> "}" => ast::BlockStmt(<>);

IfStmt: ast::IfStmt =
    "if" "(" <condition:Expr> ")" <body:BlockStmt> <else_stmt:IfOrElse?> => {
        ast::IfStmt {<>}
    };

IfOrElse: ast::IfOrElse = {
    "else" <IfStmt> => ast::IfOrElse::If(Box::new(<>)),
    "else" <BlockStmt> => ast::IfOrElse::Else(<>),
}

WhileStmt: ast::Stmt =
    "while" "(" <condition:Expr> ")" <body:BlockStmt> => ast::Stmt::WhileStmt(ast::WhileStmt {<>});

ConstDef: ast::ConstDef =
    "const" <binding:TypeBinding> "=" <value:Expr> ";" => ast::ConstDef{ <> };

VarDef: ast::VarDef =
    "var" <binding:TypeBinding> "=" <value:Expr> ";" => ast::VarDef { <> };


// EXPRESSIONS

TypeBinding: ast::TypeBinding =
    <n:Ident> ":" <t:Type> => ast::TypeBinding { name: n, ty: t};

Type: ast::Type = {
    "[" <inner:Type> ";" <len:int> "]" => ast::Type::Array(Box::new(inner), len as usize),
    "*" <Type> => ast::Type::Ref(Box::new(<>)),
    "int" => ast::Type::Int,
    "int8" => ast::Type::Int8,
    "int16" => ast::Type::Int16,
    "int32" => ast::Type::Int32,
    "int64" => ast::Type::Int64,
    "uint" => ast::Type::UInt,
    "uint8" => ast::Type::UInt8,
    "uint16" => ast::Type::UInt16,
    "uint32" => ast::Type::UInt32,
    "uint64" => ast::Type::UInt64,
    "char" => ast::Type::Char,
    "str" => ast::Type::Str,
    "cstr" => ast::Type::CStr,
    <Ident> => ast::Type::Class(<>),
}

pub Expr: ast::Expr = {
    "[" <ArrayExpr> "]",
    <ClassExpr>,
    <AndExpr>,
}

ClassExpr: ast::Expr =
    <class:Ident> "{" <fields:MaybeFieldList> "}" => {
        ast::Expr::Class(ast::ClassExpr {<>})
    };

MaybeFieldList: Vec<(String, Box<ast::Expr>)> = <FieldList?> => <>.unwrap_or(vec![]);

FieldList: Vec<(String, Box<ast::Expr>)> = {
    <mut a:FieldList> ","  <b:Field> => {a.push(b); a},
    <Field> => vec![<>],
}

Field: (String, Box<ast::Expr>) =
    <name:Ident> ":" <e:Expr> => (name, Box::new(e));

ArrayExpr: ast::Expr = {
    <ExprList> => {
        let items = <>;
        let len = items.len();
        ast::Expr::Array(items, len)
    },
    <val:Expr> ";" <len:int> => {
        let items = vec![val; len as usize];
        ast::Expr::Array(items, len as usize)
    },
}

ExprList: Vec<ast::Expr> = {
    <mut a:ExprList> ","  <b:Expr> => {a.push(b); a},
    <Expr> => vec![<>],
}

AndExpr: ast::Expr = {
    <l:AndExpr> "and" <r:OrExpr> => ast::Expr::Binary(Box::new(l), ast::BinOp::LogicalAnd, Box::new(r)),
    <OrExpr>,
}

OrExpr: ast::Expr = {
    <l:OrExpr> "or" <r:ArithmaticExpr> => ast::Expr::Binary(Box::new(l), ast::BinOp::LogicalOr, Box::new(r)),
    <AssignExpr>,
}

AssignExpr: ast::Expr = {
    <l:AssignExpr> "=" <r:CompareExpr> => ast::Expr::Binary(Box::new(l), ast::BinOp::Equals, Box::new(r)),
    <CompareExpr>,
}

CompareExpr: ast::Expr = {
    <l:CompareExpr> <op:CompareOp> <r:ArithmaticExpr> => ast::Expr::Binary(Box::new(l), op, Box::new(r)),
    <ArithmaticExpr>,
}

ArithmaticExpr: ast::Expr = {
    <l:ArithmaticExpr> <op:AddOp> <r:Term> => ast::Expr::Binary(Box::new(l), op, Box::new(r)),
    <Term>,
}

Term: ast::Expr = {
    <l:Term> <op:MulOp> <r:PrimaryExpr> => ast::Expr::Binary(Box::new(l), op, Box::new(r)),
    <CastExpr>,
}

CastExpr: ast::Expr = {
    <l:UnaryExpr> "as" <t:Type> => ast::Expr::Cast(Box::new(l), t),
    <UnaryExpr>,
}

// Unary expressions:
UnaryExpr: ast::Expr = {
    <op:UnaryOp> <value:FunctionCall> => ast::Expr::Unary(op, Box::new(value)),
    <FunctionCall>,
}

// TODO: name could just be an identifier
FunctionCall: ast::Expr = {
    <name:FunctionCall> "(" <args:MaybeArgsList> ")" => ast::Expr::FunctionCall(ast::FunctionCall { name: Box::new(name), args: args}),
    <IndexExpr>,
}

IndexExpr: ast::Expr = {
    <name:IndexExpr> "[" <index:Expr> "]" => ast::Expr::IndexExpr(Box::new(name), Box::new(index)),
    <PrimaryExpr>,
}

MaybeArgsList: Vec<ast::Expr> = <ArgsList?> => <>.unwrap_or(vec![]);

ArgsList: Vec<ast::Expr> = {
    <mut a:ArgsList> ","  <b:Expr> => {a.push(b); a},
    <Expr> => vec![<>],
}

PrimaryExpr: ast::Expr = {
    <Ident> => ast::Expr::Ident(<>),
    <IntLiteral> => ast::Expr::Literal(<>),
    <StringLiteral> => ast::Expr::Literal(<>),
    <BooleanLiteral> => ast::Expr::Literal(<>),
    "(" <Expr> ")",
}

CompareOp: ast::BinOp = {
    ">" => ast::BinOp::Gt,
    ">=" => ast::BinOp::Gte,
    "<" => ast::BinOp::Lt,
    "<=" => ast::BinOp::Lte,
    "==" => ast::BinOp::Eq,
    "!=" => ast::BinOp::Ne,
}

AddOp: ast::BinOp = {
    "+" => ast::BinOp::Plus,
    "-" => ast::BinOp::Minus,
}

MulOp: ast::BinOp = {
    "*" => ast::BinOp::Times,
    "/" => ast::BinOp::Divide,
}

UnaryOp: ast::UnaryOp = {
    "&" => ast::UnaryOp::Reference,
    "-" => ast::UnaryOp::Minus,
    "not" => ast::UnaryOp::Not,
}

Literal: ast::Literal = {
    <IntLiteral>,
    <StringLiteral>,
    <BooleanLiteral>,
}

IntLiteral: ast::Literal = {
    <int> => ast::Literal::Int(<>),
    <uint> => ast::Literal::UInt(<>),
    <int8> => ast::Literal::Int8(<>),
    <int16> => ast::Literal::Int16(<>),
    <int32> => ast::Literal::Int32(<>),
    <int64> => ast::Literal::Int64(<>),
    <uint8> => ast::Literal::UInt8(<>),
    <uint16> => ast::Literal::UInt16(<>),
    <uint32> => ast::Literal::UInt32(<>),
    <uint64> => ast::Literal::UInt64(<>),
}

StringLiteral: ast::Literal = {
    <string> => ast::Literal::Str(<>),
    <cstring> => ast::Literal::CStr(<>),
}

BooleanLiteral: ast::Literal = {
    "true" => ast::Literal::Bool(true),
    "false" => ast::Literal::Bool(true),
}

Ident: String = <ident> => <>.to_owned();
